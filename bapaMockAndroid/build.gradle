buildscript {
    ext.kotlin_version = '1.0.1'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        // Error will happen if you update gradle version to 2.0 or higher.
        classpath 'com.android.tools.build:gradle:1.3.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
    COMPILE_SDK_VERSION = 23
    BUILDTOOLS_VERSION = "23.0.1"
    MIN_SDK_VERSION = 16
    TARGET_SDK_VERSION = 23
    SUPPORT_LIBRARY_VERSION = "23.0.1"
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILDTOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    // Configure source folders like Eclipse
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            jni.srcDirs = []
            kotlin.srcDirs = ['srcKotlin']
            java.srcDirs = ['srcJava']
            resources.srcDirs = ['srcJava', 'srcKotlin']
            aidl.srcDirs = ['srcJava', 'srcKotlin']
            renderscript.srcDirs = ['srcJava', 'srcKotlin']
            res.srcDirs = ['res']

            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compile project(':ofAndroidLib')
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}

clean.dependsOn(":openFrameworksProject:clean")

assembleDebug.dependsOn(":openFrameworksProject:compileDebugOF")

assembleRelease.dependsOn(":openFrameworksProject:compileReleaseOF")
